var genrandomDebug% := 0;

' Generate a list if num% random numbers, from 0 to num%-1, suitable for choosing from an 
' array with num% elements. List is chosen so each value occurs once in num% elements. 

proc genrandomIndices(randlist%[], nrandlist%)
    var choices%[nrandlist%];
    var nchosen% := 0;
    var choicelist%[nrandlist%];
    var nchoicelist% := 0;
    var i%;
    var n%;
    
    ' initialize randlist%[]
    for i% := 0 to nrandlist%-1 do
        randlist%[i%] := -1;
        choices%[i%] := 1;
    next


    while nchosen% < nrandlist% do

        if genrandomDebug% = 1 then
            PrintLog("====== nchosen=" + str$(nchosen%) + " nrandlist=" + str$(nrandlist%) + "\n");
        endif
        
        ' fill choicelist% with indexes not already chosen.
        nchoicelist% := 0;
        for i% := 0 to nrandlist%-1 do
            if choices%[i%] = 1 then
                choicelist%[nchoicelist%] := i%;
                nchoicelist% := nchoicelist% + 1;
            endif
        next
        
        if genrandomDebug% = 1 then
            PrintLog("Filled choicelist with " + str$(nchoicelist%) + " choices\n");
            for i% := 0 to nchoicelist%-1 do
                PrintLog(str$(i%) + " ");
            next
            PrintLog("\n");
        endif
        
        ' Pick random numbers from 0-nchoicelist%, and round down to next lowest int. 
        ' That gives us an index into choicelist%[]. If the value in choicelist%[] at that 
        ' index is >= 0 it means it hasn't been chosen yet. Add that index to randlist% and 
        ' set choices%[index]=0 to indicate that value has been chosen, and set 
        ' choicelist%[index]=-1
        '
        ' Once we hit an index where the value in choicelist%[index]==-1, we give up and 
        ' regenerate choicelist%[] from the remaining choices. 
        
        n% := Floor(Rand(nchoicelist%, 0));
        if genrandomDebug% = 1 then
            PrintLog("Rand=" + str$(n%) + "\n");
        endif
        while choicelist%[n%] >= 0 do
            if genrandomDebug% = 1 then
                PrintLog("Chose ind=" + str$(n%) + " choicelist[n%]=" + str$(choicelist%[n%]) + "\n"); 
            endif
            randlist%[nchosen%] := choicelist%[n%];
            nchosen% := nchosen% + 1;
            choices%[choicelist%[n%]] := 0;
            choicelist%[n%] := -1;
            ' get another random number
            n% := Floor(Rand(nchoicelist%, 0));
            if genrandomDebug% = 1 then
                PrintLog("Rand=" + str$(n%) + "\n");
            endif
        wend
        if genrandomDebug% = 1 then
            PrintLog("Chose ind=" + str$(n%) + " ALREADY CHOSEN. Re-fill list.\n");
        endif
        
        
    wend
end