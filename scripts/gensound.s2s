' gensound.s2s
'
' These are utility functions for generating arrays of numbers suitable for sound generation with the 1401. 
' There is a means to have a sound "generated" by reading DAC values from one of a list of files, 
' selected at random. Make sure to call gensoundInitRandomList first, then each call gensoundRandomFromList
' cycles through the list generated using the directory and file mask you provide. The list is randomized, 
' and every file in the list will be used once before running through the list again. Note that the list is 
' NOT randomized again once you've run through it completely.
'
' DAC values in files should be 16 bit signed integers, values -32768<x<32767.  
'
' $Log$
' Revision 1.5  2008/04/16 18:24:31  devel
' Fixes in gensoundRandomFromList. Must save dir from gensoundInitRandomList, then cd to it when opening files.
'
' Revision 1.4  2008-03-26 17:16:14  devel
' Change function names to gensound*. Fix bug in gensoundInitRandomList when
' no noise files found.
'
'


const gensoundpi := 3.141592653589793;
const gensoundMaxFiles% := 400;
var gensoundFileDir$;
var gensoundFileList$[gensoundMaxFiles%];
var gensoundRandom%[gensoundMaxFiles%];
var gensoundCurrentIndex% := 0;
var gensoundNumFileList% := 0;
var gensoundDebug% := 0;
const gensoundIntMax := 32767;
const gensoundRampLengthMS% := 5;
#include "genrandom.s2s"

proc gensoundSetDebug(debug%)
    if debug% = 0 then
        gensoundDebug% := 0;
    else
        gensoundDebug% := 1;
    endif
end

' Generates a list of files and randomizes the list. Subsequent calls to gensoundRandomFromList
' will open the next file in the list and read/return the (int16) values. Looks in soundsDir$ for 
' files matching the soundsMask$ (e.g. noise*.bin, or noise???.bin - see FileList() in spike2 doc). 

func gensoundInitRandomList%(soundsDir$, soundsMask$)

    if gensoundDebug% = 1 then
        PrintLog("gensoundInitRandomList: Loading files from " + soundsDir$ + " using mask " + soundsMask$ + "\n");
    endif
    
    ' Probably only have to do something if NOISE will be required - load a file list or
    ' something. 

    if Len(soundsDir$) > 0 then
        var sdirtmp$;   ' Save current dir
        sdirtmp$ := FilePath$(0);       ' save current dir
        if FilePathSet(soundsDir$, 0) < 0 then
            Message("Cannot set dir to sounds dir \"" + soundsDir$ + "\"");
        else
            gensoundFileDir$ := soundsDir$;
            gensoundNumFileList% := FileList(gensoundFileList$[], -1, soundsMask$);
            if gensoundNumFileList% > 0 then
                genrandomIndices(gensoundRandom%[], gensoundNumFileList%);
            endif
            FilePathSet(sdirtmp$, 0);       ' restore dir
        endif
    endif
    
    if gensoundDebug% = 1 then
        PrintLog("gensoundInitRandomList: Loaded " + str$(gensoundNumFileList%) + " sound files.\n");
    endif

    return gensoundNumFileList%;
    
end

' Generate sz% monotone voltage values in array v[]. The last value, v[sz%-1], is set to 0 
' (since the 1401 DAC will be left at the last value after the waveform has been 
' played). The waveform will be a sinusoid, with freq 'toneHz'. The DAC output 
' sampling rate should be set in 'sampleHz'. 

Proc gensoundTone(v%[], sz%, toneHz, sampleHz)

    var i%;
    var rampValue;
    var rampLength%;
    
    rampLength% := gensoundRampLengthMS% * sampleHz / 1000;
    for i% := 0 to sz%-1 do

        ' Warning: ramp value must begin and end as a 0!!!
        docase 
        case i% < rampLength% then
            rampValue := i%/rampLength%;
        case i% > sz% - rampLength% - 1 then
            rampValue := (sz% - i% - 1)/rampLength%;
        else
            rampValue := 1.0;
        endcase
            
        v%[i%] := rampValue * gensoundIntMax * sin(2*gensoundpi*toneHz*i%/sampleHz);
    next

end


' Generate sz% voltage values in array v[] representing noise for space1g. 
' The last value, v[sz%-1], is set to 0 
' (since the 1401 DAC will be left at the last value after the waveform has been 
' played). The DAC output sampling rate should be set in 'sampleHz'. 

Proc gensoundNoise(v[], sz%, vmin, vmax, sampleHz)
    var i%;
    
    ' dummy here just set 0's.
    for i% := 0 to sz%-1 do
        v[i%] := 0;
    next

end



' Gets at most sz% voltage values in array v%[] representing noise for space1g. 
' Values are fetched from a file in the list obtained in GenerateSoundInit. The 
' filename used is returned in file$. 
' The array v% is set to 0 initially, and the number of values read is returned, neg on error. 

func gensoundRandomFromList%(v%[], sz%, &file$)
    var status% := 0;
    var handle;
    var nread% := 0;
    var dirtmp$;
    
    if gensoundNumFileList% <= 0 then
        status% := -1;
        PrintLog("gensoundRandomFromList: List is empty! Did you call gensoundInitRandomList()?");
        return status%;
    endif
    
    gensoundCurrentIndex% := gensoundCurrentIndex% + 1;
    if gensoundCurrentIndex% = gensoundNumFileList% then
        gensoundCurrentIndex% := 0;
    endif

    ' Open file
    dirtmp$ := FilePath$(0);       ' save current dir
    if FilePathSet(gensoundFileDir$, 0) < 0 then
        PrintLog("gensoundRandomFromList%: Cannot change dir to " + gensoundFileDir$ + ". Did you call gensoundInitRandomList?\n");
        status% := -1;
    else
        file$ := gensoundFileList$[gensoundRandom%[gensoundCurrentIndex%]];
        PrintLog("file$ := " + file$ + "\n");
        handle := FileOpen(file$, 9);
        if handle < 0 then
            status% := -1;
        else
            ArrConst(v%[], 0);
            status% := BReadSize(2, v%[]);
            FileClose(handle);
        endif
        FilePathSet(dirtmp$, 0);
    endif

    return status%;
end


